apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 32
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 32
        versionCode 4
        versionName "2.0.3"

        testInstrumentationRunner "AndroidJUnit4"
        consumerProguardFiles 'consumer-rules.pro'

        buildConfigField 'int', 'VERSION_CODE', "${versionCode}"
        buildConfigField 'String', 'VERSION_NAME', "\"${versionName}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    defaultPublishConfig "developmentRelease"

    flavorDimensions "avoEnvironment"

    productFlavors {
        production {
            dimension "avoEnvironment"
        }
        development {
            dimension "avoEnvironment"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    developmentApi 'com.github.avohq:android-analytics-debugger:1.0.6'

    compileOnly 'androidx.appcompat:appcompat:1.2.0'
    testCompileOnly 'androidx.appcompat:appcompat:1.2.0'
    testImplementation 'junit:junit:4.12'
    // for a specific test case with segment
    testImplementation 'com.segment.analytics.android:analytics:4.8.2'
    testImplementation 'org.json:json:20190722'
    testImplementation "org.mockito:mockito-core:2.28.2"
}

afterEvaluate {
    publishing {
        publications {
            production(MavenPublication) {
                from components.productionRelease

                groupId = 'com.github.avohq'
                artifactId = 'prod'
                version = '2.0.3'
            }

            development(MavenPublication) {
                from components.developmentRelease

                groupId = 'com.github.avohq'
                artifactId = 'dev'
                version = '2.0.3'
            }
        }
    }
}
