apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'com.vanniktech.maven.publish.base'
apply plugin: 'signing'

import com.vanniktech.maven.publish.SonatypeHost

// Detect if building on JitPack
def isJitPackBuild = System.getenv('JITPACK') == 'true'

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    if (!isJitPackBuild) {
        signAllPublications()
    }
}

android {
    compileSdk 34
    buildToolsVersion = "35.0.0"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 8
        versionName "2.4.0"

        testInstrumentationRunner "AndroidJUnit4"
        consumerProguardFiles 'consumer-rules.pro'

        buildConfigField 'int', 'VERSION_CODE', "${versionCode}"
        buildConfigField 'String', 'VERSION_NAME', "\"${versionName}\""

        aarMetadata {
            minCompileSdk = 21
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions += "avoEnvironment"

    productFlavors {
        production {
            dimension "avoEnvironment"
        }
        development {
            dimension "avoEnvironment"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildFeatures {
        buildConfig true
    }

    publishing {
        multipleVariants {
            allVariants()
            withJavadocJar()
        }
    }

    namespace 'app.avo.inspector'
}

// Choose dependency based on build environment
// JitPack builds use JitPack dependency, all other builds use Maven Central
def analyticsDebuggerDep = isJitPackBuild
    ? 'com.github.avohq:android-analytics-debugger:1.2.0'  // JitPack
    : 'app.avo:analytics-debugger:1.2.0'                    // Maven Central

dependencies {
    implementation 'androidx.annotation:annotation-jvm:1.8.2'
    developmentApi analyticsDebuggerDep

    testImplementation 'junit:junit:4.13.2'
    // for a specific test case with segment
    testImplementation 'com.segment.analytics.android:analytics:4.10.1'
    testImplementation 'org.json:json:20240303'
    testImplementation "org.mockito:mockito-core:2.28.2"
}

afterEvaluate {
    publishing {
        publications {
            // Maven Central publications
            production(MavenPublication) {
                from components.productionRelease
                groupId = 'app.avo'
                artifactId = 'inspector'
                version = android.defaultConfig.versionName

                pom {
                    name = 'Avo Inspector'
                    description = 'Avo Inspector SDK for Android - validates analytics implementation'
                    url = 'https://github.com/avohq/android-avo-inspector'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'avohq'
                            name = 'Avo'
                            email = 'friends@avo.app'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/avohq/android-avo-inspector.git'
                        developerConnection = 'scm:git:ssh://github.com/avohq/android-avo-inspector.git'
                        url = 'https://github.com/avohq/android-avo-inspector'
                    }
                }
            }

            development(MavenPublication) {
                from components.developmentRelease
                groupId = 'app.avo'
                artifactId = 'inspector-dev'
                version = android.defaultConfig.versionName

                pom {
                    name = 'Avo Inspector (Development)'
                    description = 'Avo Inspector SDK for Android with Visual Inspector'
                    url = 'https://github.com/avohq/android-avo-inspector'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'avohq'
                            name = 'Avo'
                            email = 'friends@avo.app'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/avohq/android-avo-inspector.git'
                        developerConnection = 'scm:git:ssh://github.com/avohq/android-avo-inspector.git'
                        url = 'https://github.com/avohq/android-avo-inspector'
                    }
                }
            }

            // JitPack publications (only created on JitPack builds)
            if (isJitPackBuild) {
                prod(MavenPublication) {
                    from components.productionRelease
                    groupId = 'com.github.avohq'
                    artifactId = 'prod'
                    version = android.defaultConfig.versionName
                }

                dev(MavenPublication) {
                    from components.developmentRelease
                    groupId = 'com.github.avohq'
                    artifactId = 'dev'
                    version = android.defaultConfig.versionName
                }
            }
        }
    }

    if (!isJitPackBuild) {
        signing {
            sign publishing.publications.production
            sign publishing.publications.development
        }
    }
}
