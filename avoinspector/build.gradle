apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdk 34
    buildToolsVersion = "35.0.0"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 4
        versionName "2.1.0"

        testInstrumentationRunner "AndroidJUnit4"
        consumerProguardFiles 'consumer-rules.pro'

        buildConfigField 'int', 'VERSION_CODE', "${versionCode}"
        buildConfigField 'String', 'VERSION_NAME', "\"${versionName}\""

        aarMetadata {
            minCompileSdk = 21
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions += "avoEnvironment"

    productFlavors {
        create("production") {
            dimension "avoEnvironment"
        }
        create("development") {
            dimension "avoEnvironment"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildFeatures {
        buildConfig true
    }

    namespace 'app.avo.inspector'

    publishing {
        multipleVariants {
            allVariants()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation-jvm:1.8.2'
    developmentApi 'com.github.avohq:android-analytics-debugger:1.2.0'

    testImplementation 'junit:junit:4.13.2'
    // for a specific test case with segment
    testImplementation 'com.segment.analytics.android:analytics:4.10.1'
    testImplementation 'org.json:json:20240303'
    testImplementation "org.mockito:mockito-core:2.28.2"
}

afterEvaluate {
    publishing {
        publications {
            production(MavenPublication) {
                from components.findByName('productionRelease')

                groupId = 'com.github.avohq'
                artifactId = 'prod'
                version = '2.0.9'
            }

            development(MavenPublication) {
                from components.findByName('developmentRelease')

                groupId = 'com.github.avohq'
                artifactId = 'dev'
                version = '2.0.9'
            }
        }
    }
}
