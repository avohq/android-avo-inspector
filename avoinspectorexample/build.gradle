apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdk 34
    buildToolsVersion = "35.0.0"
    defaultConfig {
        applicationId "app.avo.avoinspectorexample"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 4
        versionName "4.1"
        testInstrumentationRunner "AndroidJUnit4"

        // Required when using local module (has flavor dimension)
        if (project.hasProperty('source') && project.source == 'local') {
            missingDimensionStrategy "avoEnvironment", "development", "production"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    namespace 'app.avo.avoinspectorexample'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.9.20"))

    implementation 'com.segment.analytics.android:analytics:4.10.1'

    // Avo Inspector dependency - use -Psource=local|jitpack-release|jitpack-commit|maven (default: maven)
    def dependencySource = project.hasProperty('source') ? project.source : 'maven'
    def ref = project.hasProperty('ref') ? project.ref : ''

    if (dependencySource == 'local') {
        // Local module - for development
        implementation project(path: ':avoinspector')
    } else if (dependencySource == 'jitpack-release') {
        // JitPack release builds (tags)
        debugImplementation "com.github.avohq:dev:${ref}"
        releaseImplementation "com.github.avohq:prod:${ref}"
    } else if (dependencySource == 'jitpack-commit') {
        // JitPack commit builds (commit hash or branch)
        debugImplementation "com.github.avohq.android-avo-inspector:inspector-dev:${ref}"
        releaseImplementation "com.github.avohq.android-avo-inspector:inspector:${ref}"
    } else {
        // Maven Central (default) - for released versions
        debugImplementation 'app.avo:inspector-dev:2.3.0'
        releaseImplementation 'app.avo:inspector:2.3.0'
    }

    testImplementation 'org.json:json:20240303'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:2.28.2"
}
repositories {
    mavenCentral()
    // JitPack repository for jitpack-release and jitpack-commit builds
    if (project.hasProperty('source') && (project.source == 'jitpack-release' || project.source == 'jitpack-commit')) {
        maven { url 'https://jitpack.io' }
    }
}
