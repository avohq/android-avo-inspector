apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdk 34
    buildToolsVersion = "35.0.0"
    defaultConfig {
        applicationId "app.avo.avoinspectorexample"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 4
        versionName "4.1"
        testInstrumentationRunner "AndroidJUnit4"

        // Load API key from local.properties (gitignored), fall back to empty string
        def localProps = new Properties()
        def localPropsFile = rootProject.file('local.properties')
        if (localPropsFile.exists()) {
            localProps.load(localPropsFile.newDataInputStream())
        }
        buildConfigField "String", "AVO_API_KEY", "\"${localProps.getProperty('avoApiKey', '')}\""

        // Required when using local module (has flavor dimension)
        if (project.hasProperty('source') && project.source == 'local') {
            missingDimensionStrategy "avoEnvironment", "development", "production"
        }
    }
    buildFeatures {
        buildConfig true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    namespace 'app.avo.avoinspectorexample'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.9.20"))

    implementation 'com.segment.analytics.android:analytics:4.10.1'

    // Avo Inspector dependency - use -Psource=local|jitpack|maven (default: maven)
    def dependencySource = project.hasProperty('source') ? project.source : 'maven'
    def ref = project.hasProperty('ref') ? project.ref : ''

    if (dependencySource == 'local') {
        // Local module - for development
        implementation project(path: ':avoinspector')
    } else if (dependencySource == 'jitpack') {
        // JitPack builds (release tags, commit hash, or branch)
        debugImplementation "com.github.avohq.android-avo-inspector:inspector-dev:${ref}"
        releaseImplementation "com.github.avohq.android-avo-inspector:inspector:${ref}"
    } else {
        // Maven Central (default) - for released versions
        debugImplementation 'app.avo:inspector-dev:2.3.0'
        releaseImplementation 'app.avo:inspector:2.3.0'
    }

    testImplementation 'org.json:json:20240303'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:2.28.2"
}
repositories {
    mavenCentral()
    // JitPack repository for jitpack builds
    if (project.hasProperty('source') && project.source == 'jitpack') {
        maven { url 'https://jitpack.io' }
    }
}
